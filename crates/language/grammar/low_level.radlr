IGNORE { c:sp c:nl tk:cmt::comment }

IMPORT comment as cmt
IMPORT primitive as prim
IMPORT expression as expr

<> low_level_function 

  > ( type "<-" :ast $1 )?^return_type tk:( id "\(" )  var_type_binding(+)^params ")" block

    :ast { t_LL_function, params, return_type, block  }


<> statements 
  
  > declaration 
  | loop 
  | match 
  | continue 
  | assignment 
  | block 


<> block 

  > "{" statements(+)^statements return?^ret "}"

    :ast { t_LL_Block, statements, ret  }


<> return 
  
  > "<-"  expression

    :ast { t_LL_Return, expression }
  

<> declaration 
  
  > var_type_binding ";"?

    :ast $1

  | id ":=" pointer_cast ";"?

    :ast { t_LL_PointerCastBinding, id, pointer_cast, tok }


<> var_type_binding 
  
  > id ":" type^ty

    :ast { t_LL_Var_Binding, id, ty, tok }


<> continue 
  
  > "continue" ";"?
    
    :ast { t_LL_Continue, tok }


<> id 
  
  > prim::id^id

    :ast { t_Id, id, tok }


<> loop 
  
  > "loop" block

    :ast { t_LL_Loop, block, tok  }


<> match 

  > "match" ( boolean | arithmetic )^expression  "{" match_statement(+)^statements "}"

    :ast { t_LL_Match, expression, statements, tok  }
  

<> match_statement 

  > match_case 

  | block


<> match_case 
  
  > ( primitive_bool | primitive_number ) block

    :ast { t_LL_MatchCase, val:$1, block, tok  }


<> assignment 

  > (id | memory_location ) "=" expression ";"?

    :ast { t_LL_Assign, location: $1, expression, tok  }

  | id "<-" "heap(" (primitive_unsigned_integer | id)^byte_count ")" ";"? 

    :ast { t_LL_HeapAllocate, id, byte_count, tok  }

  | id "<-" "stack(" (primitive_unsigned_integer | id)^byte_count ")" ";"? 

    :ast { t_LL_StackAllocate, id, byte_count, tok  }

  | "free(" id ")" ";"?

    :ast { t_LL_Free, id  }


<> call > id "(" expression(+",") ")"

  :ast { t_LL_Call, id  }


<> member > id^root ("." id )(*)^branches

  :ast { t_LL_Member, root, branches }


<> self 

  > tk:( "self." ) id

    :ast { t_LL_SelfMember, id }


<> type 
  
  > primitive_type

  | primitive_ptr_type

  | table_type


<> primitive_bool 

  > prim::true

    :ast { t_LL_True }

  | prim::false

    :ast { t_LL_False }

<> primitive_value

  > primitive_number

  | prim::string^val

    :ast { t_LL_Str, val:str($val)  }


<> primitive_number 

  > prim::sci_num

    :ast { t_LL_Num, val:f64($1), tok }


<> primitive_unsigned_integer 

  > prim::uint

    :ast { t_LL_Uint, val:i64($1) }


<> primitive_integer 

  > prim::int

    :ast { t_LL_Int, val:i64($1) }


<> table_type

  > "table" :ast { t_Type_Table }
  | "row"   :ast { t_Type_Row }


<> primitive_ptr_type

  > "*8"    :ast { t_Type_8BitPointer }
  | "*16"   :ast { t_Type_16BitPointer }
  | "*32"   :ast { t_Type_32BitPointer }
  | "*64"   :ast { t_Type_64BitPointer }
  | "*128"  :ast { t_Type_128BitPointer }


<> primitive_type

  > "u8"    :ast { t_Type_u8 }
  | "i8"    :ast { t_Type_i8 }
  | "u16"   :ast { t_Type_u16 }
  | "i16"   :ast { t_Type_i16 }
  | "u32"   :ast { t_Type_u32 }
  | "i32"   :ast { t_Type_i32 }
  | "u64"   :ast { t_Type_u64 }
  | "i64"   :ast { t_Type_i64 }
  | "f32"   :ast { t_Type_f32 }
  | "f64"   :ast { t_Type_f64 }
  | "f128"  :ast { t_Type_f128 }
  | "f32v2" :ast { t_Type_f32v2 }
  | "f32v3" :ast { t_Type_f32v3 }
  | "f32v4" :ast { t_Type_f32v4 }
  | "f32v8" :ast { t_Type_f32v8 }
  | "f64v2" :ast { t_Type_f64v2 }
  | "f64v4" :ast { t_Type_f64v4 }


<> expression 

  > arithmetic


<> boolean 

  > boolean^left "||"{1}  boolean{1}^right
    
    :ast { t_OR, left, right, tok } 

  | boolean^left "|||"{2}  boolean{2}^right
    
    :ast { t_XOR, left, right, tok } 

  | boolean^left "&&"{3}  boolean{3}^right
    
    :ast { t_AND, left, right, tok }

  | logical

  | "(" boolean ")"
    
    :ast $2


<> logical 

  > arithmetic^left "=="{3}  arithmetic{3}^right
    
    :ast { t_EQ, left, right, tok } 

  | arithmetic^left "!="{3}  arithmetic{3}^right
    
    :ast { t_NE, left, right, tok } 

  | arithmetic^left ">="{3}  arithmetic{3}^right
    
    :ast { t_GE, left, right, tok } 

  | arithmetic^left "<="{3}  arithmetic{3}^right
    
    :ast { t_LE, left, right, tok } 

  | arithmetic^left  ">"{3}  arithmetic{3}^right
    
    :ast { t_GR, left, right, tok } 

  | arithmetic^left  "<"{3}  arithmetic{3}^right
    
    :ast { t_LS, left, right, tok } 

  | "!"{4} logical^expr{3}
    
    :ast { t_NOT, expr } 


<> bitwise 

  > bitwise^left  ">>"{0}  bitwise{0}^right
    
    :ast { t_BIT_SHIFT_RIGHT, left, right } 

  | bitwise^left  "<<"{0}  bitwise{0}^right
    
    :ast { t_BIT_SHIFT_LEFT, left, right } 

  | bitwise^left "&"{0}  bitwise{0}^right
    
    :ast { t_BIT_AND, left, right } 

  | bitwise^left "|"{0}  bitwise{0}^right
    
    :ast { t_BIT_OR, left, right } 

  | bitwise^left "~|"{0}  bitwise{0}^right
    
    :ast { t_BIT_EXC_OR, left, right } 

  | arithmetic


<> arithmetic 
  
  > arithmetic^left   "+"{1}  arithmetic{1}^right
    
    :ast { t_Add, right, left, tok }

  | arithmetic^left   "-"{1}  arithmetic{1}^right
    
    :ast { t_Sub, right, left, tok }

  | arithmetic^left   "*"{2}  arithmetic{2}^right
    
    :ast { t_Mul, right, left, tok }

  | arithmetic^left   "/"{2}  arithmetic{2}^right
    
    :ast { t_Div, right, left, tok }

  | arithmetic^left   "%"{2}  arithmetic{2}^right
    
    :ast { t_Mod, right, left, tok }

  | arithmetic^left  "**"{3}  arithmetic{3}^right
    
    :ast { t_Pow, right, left, tok }

  | arithmetic^left  "//"{3}  arithmetic{3}^right
    
    :ast { t_Root, right, left, tok }

  | arithmetic^left "log"{3}  arithmetic{3}^right
    
    :ast { t_Log, right, left, tok }

  | Term

  | "(" arithmetic ")" 
    
    :ast $2


<> Term 

  > call | member | primitive_value | memory_location | self | primitive_cast


<> memory_location 

  > "[" pointer_offset "]"

    :ast $2


<> pointer_offset 

  > (pointer_cast | id)^base_ptr ( "+" mem_expr )?^expression

    :ast { t_LL_MemLocation, base_ptr, expression, tok }


<> mem_expr 

  > mem_expr^left "+"{1} mem_expr{1}^right

    :ast { t_LL_MemAdd, left, right, tok }

  | mem_expr^left "*"{2} mem_expr{2}^right

    :ast { t_LL_MemMul, left, right, tok }

  | id

  | primitive_integer


<> primitive_cast 

  > primitive_type^ty "(" expression ")"

    :ast { t_LL_PrimitiveCast, ty, expression, tok }


<> pointer_cast 

  > primitive_ptr_type^ty "(" id ")"

    :ast { t_LL_PointerCast, ty, id, tok }